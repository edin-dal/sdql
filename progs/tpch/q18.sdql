let customer = load[{<c_custkey: int, c_name: string, c_address: string, c_nationkey: int, c_phone: string, c_acctbal: double, c_mktsegment: string, c_comment: string> -> int}]("datasets/tpch/customer.tbl")

let orders = load[{<o_orderkey: int, o_custkey: int, o_orderstatus: string, o_totalprice: double, o_orderdate: date, o_orderpriority: string, o_clerk: string, o_shippriority: int, o_comment: string> -> int}]("datasets/tpch/orders.tbl")

let lineitem = load[{<l_orderkey: int, l_partkey: int, l_suppkey: int, l_linenumber: int, l_quantity: double, l_extendedprice: double, l_discount: double, l_tax: double, l_returnflag: string, l_linestatus: string, l_shipdate: date, l_commitdate: date, l_receiptdate: date, l_shipinstruct: string, l_shipmode: string, l_comment: string> -> int}]("datasets/tpch/lineitem.tbl")

// TODO fix so we can reuse variable names <l,l_v>

let la_h =
  sum(<la,la_v> in lineitem)
    { la.l_orderkey -> la.l_quantity }

let orderkeys =
  sum(<l_orderkey,l_quantity> in la_h)
    if(300 < l_quantity) then
      { l_orderkey -> true }
    else
      { }

let custkey_to_name =
  sum(<c,c_v> in customer)
    { c.c_custkey -> < _ = c.c_name > }

let o_h =
  sum(<o,o_v> in orders)
    if((orderkeys(o.o_orderkey) != {}) && (custkey_to_name(o.o_custkey) != {})) then
      {
        o.o_orderkey ->
        <
          name = custkey_to_name(o.o_custkey)(0),
          custkey = o.o_custkey,
          orderkey = o.o_orderkey,
          orderdate = o.o_orderdate,
          totalprice = o.o_totalprice
        >
      }
    else
      { }

let lb_h =
  sum(<lb,lb_v> in lineitem)
    if(o_h(lb.l_orderkey) != {}) then
      { o_h(lb.l_orderkey) -> < _ = lb.l_quantity > }
    else
      { }

sum(<k,v> <- lb_h)
  { concat(k,v) -> 1 }
