let part = load[{<p_partkey: int, p_name: varchar(55), p_mfgr: varchar(25), p_brand: varchar(10), p_type: varchar(25), p_size: int, p_container: varchar(10), p_retailprice: double, p_comment: varchar(23)> -> int}]("datasets/tpch/part.tbl")

let supplier = load[{<s_suppkey: int, s_name: varchar(25), s_address: varchar(40), s_nationkey: int, s_phone: varchar(15), s_acctbal: double, s_comment: varchar(101)> -> int}]("datasets/tpch/supplier.tbl")

let lineitem = load[{<l_orderkey: int, l_partkey: int, l_suppkey: int, l_linenumber: int, l_quantity: double, l_extendedprice: double, l_discount: double, l_tax: double, l_returnflag: varchar(1), l_linestatus: varchar(1), l_shipdate: date, l_commitdate: date, l_receiptdate: date, l_shipinstruct: varchar(25), l_shipmode: varchar(10), l_comment: varchar(44)> -> int}]("datasets/tpch/lineitem.tbl")

let partsupp = load[{<ps_partkey: int, ps_suppkey: int, ps_availqty: double, ps_supplycost: double, ps_comment: varchar(199)> -> int}]("datasets/tpch/partsupp.tbl")

let orders = load[{<o_orderkey: int, o_custkey: int, o_orderstatus: varchar(1), o_totalprice: double, o_orderdate: date, o_orderpriority: varchar(15), o_clerk: varchar(15), o_shippriority: int, o_comment: varchar(79)> -> int}]("datasets/tpch/orders.tbl")

let nation = load[{<n_nationkey: int, n_name: varchar(25), n_regionkey: int, r_comment: varchar(152)> -> int}]("datasets/tpch/nation.tbl")

let green = ext(`ConstantString`,"green", 6)

let n_h =
  sum(<n,n_v> in nation)
    { unique(n.n_nationkey) -> < _ = n.n_name > }

let s_h =
  sum(<s,s_v> in supplier)
    { unique(s.s_suppkey) -> n_h(s.s_nationkey)(0) }

let p_h =
  sum(<p,p_v> in part)
    if(ext(`StrContains`, p.p_name, green)) then
      { unique(p.p_partkey) -> < _ = p.p_partkey > }
    else
      { }

let ps_h =
  sum(<ps,ps_v> in partsupp)
    if(p_h(ps.ps_partkey) != {}) then
      { unique(< _ = ps.ps_partkey, _ = ps.ps_suppkey >) -> < _ = s_h(ps.ps_suppkey), _ = ps.ps_supplycost > }
    else
      { }

let o_h =
  sum(<o,o_v> in orders)
    { o.o_orderkey -> o.o_orderdate }

let li_h =
  sum(<li,li_v> <- lineitem)
    if(ps_h(< _ = li.l_partkey, _ = li.l_suppkey >) != {}) then
      {
        <
          nation = ps_h(< _ = li.l_partkey, _ = li.l_suppkey >)(0),
           o_year =  o_h(li.l_orderkey) / 10000
        > ->
         < um_profit = li.l_extendedprice * (1.0 - li.l_discount) - ps_h(< _ = li.l_partkey, _ = li.l_suppkey >)(1) * li.l_quantity >
      }
    else
      { }

sum(<k,v> <- li_h)
  { unique(concat(k,v)) -> 1 }
