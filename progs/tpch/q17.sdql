let lineitem = load[{<l_orderkey: int, l_partkey: int, l_suppkey: int, l_linenumber: int, l_quantity: double, l_extendedprice: double, l_discount: double, l_tax: double, l_returnflag: string, l_linestatus: string, l_shipdate: date, l_commitdate: date, l_receiptdate: date, l_shipinstruct: string, l_shipmode: string, l_comment: string> -> int}]("datasets/tpch/lineitem.tbl")

let part = load[{<p_partkey: int, p_name: string, p_mfgr: string, p_brand: string, p_type: string, p_size: int, p_container: string, p_retailprice: double, p_comment: string> -> int}]("datasets/tpch/part.tbl")

let p_h =
  for(<p,p_v> <- part)
    if((p.p_brand == "Brand#23") && (p.p_container == "MED BOX")) then
      { p.p_partkey -> < _ = p.p_partkey > }
    else
      { }

// TODO avoid redefining l,l_v twice in codegen

let l_h =
  for(<la,la_v> <- lineitem)
    if(la.l_partkey ∈ p_h) then
      { la.l_partkey -> < _ = la.l_quantity, _ = 1.0 > }
    else
      { }

let tot =
  sum(<lb,lb_v> <- lineitem)
    let avg = 0.2 * l_h(lb.l_partkey)(0) / l_h(lb.l_partkey)(1)
    if((lb.l_partkey ∈ l_h) && (lb.l_quantity < avg )) then
      lb.l_extendedprice
    else
      0.0

tot / 7.0
