let lineitem = load[{<l_orderkey: int, l_partkey: int, l_suppkey: int, l_linenumber: int, l_quantity: double, l_extendedprice: double, l_discount: double, l_tax: double, l_returnflag: varchar(1), l_linestatus: varchar(1), l_shipdate: date, l_commitdate: date, l_receiptdate: date, l_shipinstruct: varchar(25), l_shipmode: varchar(10), l_comment: varchar(44)> -> int}]("datasets/tpch/lineitem.tbl")

let supplier = load[{<s_suppkey: int, s_name: varchar(25), s_address: varchar(40), s_nationkey: int, s_phone: varchar(15), s_acctbal: double, s_comment: varchar(101)> -> int}]("datasets/tpch/supplier.tbl")

let suppkey_to_revenue =
  sum(<l,l_v> in lineitem)
    if((date(19960101) <= l.l_shipdate) && (l.l_shipdate < date(19960401))) then
      { l.l_suppkey -> l.l_extendedprice * (1.0 - l.l_discount) }
    else
      { }

let max_revenue = ext(`MaxValue`, suppkey_to_revenue)

let suppkey_to_supp =
  sum(<s,s_v> in supplier)
    {
      unique(s.s_suppkey) ->
      <
        name = s.s_name,
        address = s.s_address,
        phone = s.s_phone
      >
    }

sum(<suppkey,revenue> in suppkey_to_revenue)
  if(revenue == max_revenue) then
    {
      <
        suppkey = suppkey,
        name = suppkey_to_supp(suppkey).name,
        address = suppkey_to_supp(suppkey).address,
        phone = suppkey_to_supp(suppkey).phone,
        total_revenue = revenue
      > -> 1
    }
  else
    { }
