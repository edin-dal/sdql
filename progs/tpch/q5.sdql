let customer = load[{<c_custkey: int, c_name: varchar(25), c_address: varchar(40), c_nationkey: int, c_phone: varchar(15), c_acctbal: double, c_mktsegment: varchar(10), c_comment: varchar(117)> -> int}]("datasets/tpch/customer.tbl")

let orders = load[{<o_orderkey: int, o_custkey: int, o_orderstatus: varchar(1), o_totalprice: double, o_orderdate: date, o_orderpriority: varchar(15), o_clerk: varchar(15), o_shippriority: int, o_comment: varchar(79)> -> int}]("datasets/tpch/orders.tbl")

let lineitem = load[{<l_orderkey: int, l_partkey: int, l_suppkey: int, l_linenumber: int, l_quantity: double, l_extendedprice: double, l_discount: double, l_tax: double, l_returnflag: varchar(1), l_linestatus: varchar(1), l_shipdate: date, l_commitdate: date, l_receiptdate: date, l_shipinstruct: varchar(25), l_shipmode: varchar(10), l_comment: varchar(44)> -> int}]("datasets/tpch/lineitem.tbl")

let supplier = load[{<s_suppkey: int, s_name: varchar(25), s_address: varchar(40), s_nationkey: int, s_phone: varchar(15), s_acctbal: double, s_comment: varchar(101)> -> int}]("datasets/tpch/supplier.tbl")

let nation = load[{<n_nationkey: int, n_name: varchar(25), n_regionkey: int, r_comment: varchar(152)> -> int}]("datasets/tpch/nation.tbl")

let region = load[{<r_regionkey: int, r_name: varchar(25), r_comment: varchar(152)> -> int}]("datasets/tpch/region.tbl")

let asia = ext(`ConstantString`,"ASIA", 5)

let r_h =
  sum_unique(<r,r_v> in region)
    if(r.r_name == asia) then
      { r.r_regionkey -> < _ = r.r_regionkey > }
    else
      { }

let n_h =
  sum_unique(<n,n_v> in nation)
    if(r_h(n.n_regionkey) != {}) then
      { n.n_nationkey -> n.n_name }
    else
      { }

let c_h =
  sum_unique(<c,c_v> in customer)
    if(n_h(c.c_nationkey) != {}) then
      { c.c_custkey -> < _ =n_h(c.c_nationkey), _ = c.c_nationkey > }
    else
      { }

let o_h =
  sum_unique(<o,o_v> in orders)
    if(
        (o.o_orderdate < date(19950101))
        && (date(19940101) <= o.o_orderdate)
        && (c_h(o.o_custkey) != {})
      ) then
      { o.o_orderkey -> < _ = c_h(o.o_custkey)(0), _ = c_h(o.o_custkey)(1) > }
    else
      { }

let s_h =
  sum_unique(<s,s_v> in supplier)
    { < _ = s.s_suppkey, _ = s.s_nationkey > -> 1 }

let l_h =
  sum(<l, l_v> <- lineitem)
    if(
        (o_h(l.l_orderkey) != {})
        && (s_h(< _ = l.l_suppkey, _ = o_h(l.l_orderkey)(1) >) != {})
      ) then
      { o_h(l.l_orderkey)(0) -> l.l_extendedprice * (1.0 - l.l_discount) }
    else
      { }

sum_unique(<k,v> <- l_h)
  { < _ = k, _ = v > -> 1 }
