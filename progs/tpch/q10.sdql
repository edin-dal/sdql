let customer = load[{<c_custkey: int, c_name: varchar(25), c_address: varchar(40), c_nationkey: int, c_phone: varchar(15), c_acctbal: double, c_mktsegment: varchar(10), c_comment: varchar(117)> -> int}]("datasets/tpch/customer.tbl")

let orders = load[{<o_orderkey: int, o_custkey: int, o_orderstatus: varchar(1), o_totalprice: double, o_orderdate: date, o_orderpriority: varchar(15), o_clerk: varchar(15), o_shippriority: int, o_comment: varchar(79)> -> int}]("datasets/tpch/orders.tbl")

let lineitem = load[{<l_orderkey: int, l_partkey: int, l_suppkey: int, l_linenumber: int, l_quantity: double, l_extendedprice: double, l_discount: double, l_tax: double, l_returnflag: varchar(1), l_linestatus: varchar(1), l_shipdate: date, l_commitdate: date, l_receiptdate: date, l_shipinstruct: varchar(25), l_shipmode: varchar(10), l_comment: varchar(44)> -> int}]("datasets/tpch/lineitem.tbl")

let nation = load[{<n_nationkey: int, n_name: varchar(25), n_regionkey: int, r_comment: varchar(152)> -> int}]("datasets/tpch/nation.tbl")

let r = ext(`ConstantString`,"R", 2)

let n_h =
  sum_unique(<n,n_v> in nation)
    { n.n_nationkey -> < _ = n.n_name > }

let c_h =
  sum_unique(<c,c_v> in customer)
    {
      c.c_custkey ->
      <
        _ = c.c_custkey,
        _ = c.c_name,
        _ = c.c_acctbal,
        _ = c.c_address,
        _ = c.c_nationkey,
        _ = c.c_phone,
        _ = c.c_comment
      >
    }

let o_h =
  sum_unique(<o,o_v> in orders)
    if(
         (date(19931001)  <= o.o_orderdate)
         && (o.o_orderdate < date(19940101))
         && (c_h(o.o_custkey) != {})
       ) then
      {
        o.o_orderkey ->
        <
          c_custkey =c_h(o.o_custkey)(0),
          c_name =c_h(o.o_custkey)(1),
          c_acctbal =c_h(o.o_custkey)(2),
          c_address =c_h(o.o_custkey)(3),
          c_phone =c_h(o.o_custkey)(5),
          c_comment =c_h(o.o_custkey)(6),
          n_name = n_h((c_h(o.o_custkey))(4))(0)
        >
      }
      else
        { }

let l_h =
  sum(<l,l_v> in lineitem)
    if((l.l_returnflag == r) && (o_h(l.l_orderkey) != {})) then
      {
        <
          c_custkey = o_h(l.l_orderkey)(0),
          c_name = o_h(l.l_orderkey)(1),
          c_acctbal = o_h(l.l_orderkey)(2),
          n_name = o_h(l.l_orderkey)(6),
          c_address = o_h(l.l_orderkey)(3),
          c_phone = o_h(l.l_orderkey)(4),
          c_comment = o_h(l.l_orderkey)(5)
        >
        -> l.l_extendedprice * (1.0 - l.l_discount)
      }
      else
        { }

sum_unique(<k,v> in l_h)
  {
    <
      c_custkey = k.c_custkey,
      c_name = k.c_name,
      revenue = v,
      c_acctbal = k.c_acctbal,
      n_name = k.n_name,
      c_phone = k.c_phone,
      c_address = k.c_address,
      c_comment = k.c_comment
    >
    -> 1
  }
