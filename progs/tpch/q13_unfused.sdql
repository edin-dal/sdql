let customer = load[{<c_custkey: int, c_name: string, c_address: string, c_nationkey: int, c_phone: string, c_acctbal: double, c_mktsegment: string, c_comment: string> -> int}]("datasets/tpch/customer.tbl")
let orders = load[{<o_orderkey: int, o_custkey: int, o_orderstatus: string, o_totalprice: double, o_orderdate: date, o_orderpriority: string, o_clerk: string, o_shippriority: int, o_comment: string> -> int}]("datasets/tpch/orders.tbl")
let o_h = 
  sum(<o,m> in orders) 
    let idx1 = ext(`StrIndexOf`, o.o_comment, "special", 0)
    let idx2 = ext(`StrIndexOf`, o.o_comment, "requests", idx1)
    if(!((idx1 != -1) && (idx2 != -1))) then 
      {o.o_custkey -> { o -> m } } 
    else 
      {}
let c_h1 = sum(<c,m> in customer) 
  if(o_h(c.c_custkey) != {}) then
    sum(<o,m2> in o_h(c.c_custkey)) 
      { <left=c, right={o -> m2}> -> m }
  else 
    { <left=c, right={}> -> m }
let c_h2 = 
  sum(<co,m> in c_h1) 
    if(co.right != {}) then
      sum(<o,m2> in co.right) 
        { co.left.c_custkey -> promote[nullable[int]](m * m2) }
    else 
      { co.left.c_custkey -> promote[nullable[int]](0) }
sum(<o,m> in c_h2) { promote[int](m) -> 1 }