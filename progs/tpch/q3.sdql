let customer = load[{<c_custkey: int, c_name: varchar(25), c_address: varchar(40), c_nationkey: int, c_phone: varchar(15), c_acctbal: double, c_mktsegment: varchar(10), c_comment: varchar(117)> -> int}]("datasets/tpch/customer.tbl")

let orders = load[{<o_orderkey: int, o_custkey: int, o_orderstatus: varchar(1), o_totalprice: double, o_orderdate: date, o_orderpriority: varchar(15), o_clerk: varchar(15), o_shippriority: int, o_comment: varchar(79)> -> int}]("datasets/tpch/orders.tbl")

let lineitem = load[{<l_orderkey: int, l_partkey: int, l_suppkey: int, l_linenumber: int, l_quantity: double, l_extendedprice: double, l_discount: double, l_tax: double, l_returnflag: varchar(1), l_linestatus: varchar(1), l_shipdate: date, l_commitdate: date, l_receiptdate: date, l_shipinstruct: varchar(25), l_shipmode: varchar(10), l_comment: varchar(44)> -> int}]("datasets/tpch/lineitem.tbl")

let building = ext(`ConstantString`,"BUILDING", 9)

let c_h =
  sum_unique(<c,c_v> in customer)
    if(c.c_mktsegment == building) then
      { c.c_custkey -> < _ = c.c_custkey > }
    else
      { }

let o_h =
  sum_unique(<o,o_v> in orders)
    if(
        (o.o_orderdate < date(19950315))
        && (c_h(o.o_custkey) != {})
      ) then
      { o.o_orderkey -> < _ = o.o_orderdate, _ = o.o_shippriority > }
    else
      { }

let li_h =
  sum(<li,li_v> in lineitem)
    if(
        (date(19950315) < li.l_shipdate)
        && (o_h(li.l_orderkey) != {})
      ) then
      {
        <
          l_orderkey = li.l_orderkey,
          o_orderdate = o_h(li.l_orderkey)(0),
          o_shippriority = o_h(li.l_orderkey)(1)
        > ->
         < revenue = li.l_extendedprice * (1 - li.l_discount) >
      }
    else
      { }

sum_unique(<k,v> <- li_h)
  { concat(k,v) -> 1 }
