let customer = load[<c_custkey: @vec {int -> int}, c_name: @vec {int -> varchar(25)}, c_address: @vec {int -> varchar(40)}, c_nationkey: @vec {int -> int}, c_phone: @vec {int -> varchar(15)}, c_acctbal: @vec {int -> double}, c_mktsegment: @vec {int -> varchar(10)}, c_comment: @vec {int -> varchar(117)}, size: int>]("datasets/tpch/customer.tbl")
let orders = load[<o_orderkey: @vec {int -> int}, o_custkey: @vec {int -> int}, o_orderstatus: @vec {int -> varchar(1)}, o_totalprice: @vec {int -> double}, o_orderdate: @vec {int -> date}, o_orderpriority: @vec {int -> varchar(15)}, o_clerk: @vec {int -> varchar(15)}, o_shippriority: @vec {int -> int}, o_comment: @vec {int -> varchar(79)}, size: int>]("datasets/tpch/orders.tbl")
let lineitem = load[<l_orderkey: @vec {int -> int}, l_partkey: @vec {int -> int}, l_suppkey: @vec {int -> int}, l_linenumber: @vec {int -> int}, l_quantity: @vec {int -> double}, l_extendedprice: @vec {int -> double}, l_discount: @vec {int -> double}, l_tax: @vec {int -> double}, l_returnflag: @vec {int -> varchar(1)}, l_linestatus: @vec {int -> varchar(1)}, l_shipdate: @vec {int -> date}, l_commitdate: @vec {int -> date}, l_receiptdate: @vec {int -> date}, l_shipinstruct: @vec {int -> varchar(25)}, l_shipmode: @vec {int -> varchar(10)}, l_comment: @vec {int -> varchar(44)}, size: int>]("datasets/tpch/lineitem.tbl")

timer

let building = ext(`ConstantString`,"BUILDING", 9)

let c_h =
  sum(<i,_> <- range(customer.size))
    let c = <
      c_custkey=customer.c_custkey(i),
      c_name=customer.c_name(i),
      c_address=customer.c_address(i),
      c_nationkey=customer.c_nationkey(i),
      c_phone=customer.c_phone(i),
      c_acctbal=customer.c_acctbal(i),
      c_mktsegment=customer.c_mktsegment(i),
      c_comment=customer.c_comment(i)
    >
    if(c.c_mktsegment == building) then
      { unique(c.c_custkey) -> < _ = c.c_custkey > }
    else
      { }

let o_h =
  sum(<i,_> <- range(orders.size))
    let o = <
      o_orderkey=orders.o_orderkey(i),
      o_custkey=orders.o_custkey(i),
      o_orderstatus=orders.o_orderstatus(i),
      o_totalprice=orders.o_totalprice(i),
      o_orderdate=orders.o_orderdate(i),
      o_orderpriority=orders.o_orderpriority(i),
      o_clerk=orders.o_clerk(i),
      o_shippriority=orders.o_shippriority(i),
      o_comment=orders.o_comment(i)
    >
    if(
        (o.o_orderdate < date(19950315))
        && (c_h(o.o_custkey) != {})
      ) then
      { unique(o.o_orderkey) -> < _ = o.o_orderdate, _ = o.o_shippriority > }
    else
      { }

let l_h =
  sum(<i,_> <- range(lineitem.size))
    let l = <
      l_orderkey=lineitem.l_orderkey(i),
      l_partkey=lineitem.l_partkey(i),
      l_suppkey=lineitem.l_suppkey(i),
      l_linenumber=lineitem.l_linenumber(i),
      l_quantity=lineitem.l_quantity(i),
      l_extendedprice=lineitem.l_extendedprice(i),
      l_discount=lineitem.l_discount(i),
      l_tax=lineitem.l_tax(i),
      l_returnflag=lineitem.l_returnflag(i),
      l_linestatus=lineitem.l_linestatus(i),
      l_shipdate=lineitem.l_shipdate(i),
      l_commitdate=lineitem.l_commitdate(i),
      l_receiptdate=lineitem.l_receiptdate(i),
      l_shipinstruct=lineitem.l_shipinstruct(i),
      l_shipmode=lineitem.l_shipmode(i),
      l_comment=lineitem.l_comment(i)
    >
    if(
        (date(19950315) < l.l_shipdate)
        && (o_h(l.l_orderkey) != {})
      ) then
      {
        <
          l_orderkey = l.l_orderkey,
          o_orderdate = o_h(l.l_orderkey)(0),
          o_shippriority = o_h(l.l_orderkey)(1)
        > ->
         < revenue = l.l_extendedprice * (1 - l.l_discount) >
      }
    else
      { }

sum(<k,v> <- l_h)
  { unique(concat(k,v)) -> 1 }
