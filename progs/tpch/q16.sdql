let partsupp = load[{<ps_partkey: int, ps_suppkey: int, ps_availqty: double, ps_supplycost: double, ps_comment: varchar(199)> -> int}]("datasets/tpch/partsupp.tbl")

let part = load[{<p_partkey: int, p_name: varchar(55), p_mfgr: varchar(25), p_brand: varchar(10), p_type: varchar(25), p_size: int, p_container: varchar(10), p_retailprice: double, p_comment: varchar(23)> -> int}]("datasets/tpch/part.tbl")

let supplier = load[{<s_suppkey: int, s_name: varchar(25), s_address: varchar(40), s_nationkey: int, s_phone: varchar(15), s_acctbal: double, s_comment: varchar(101)> -> int}]("datasets/tpch/supplier.tbl")

let brand = ext(`ConstantString`, "Brand#45", 9)
let medpol = ext(`ConstantString`, "MEDIUM POLISHED", 16)
let _customer = ext(`ConstantString`, "Customer", 9)
let _complaints = ext(`ConstantString`, "Complaints", 11)
let nchars_customer = 8

let p_h =
  sum_unique(<p,p_v> <- part)
    if(
      (p.p_brand != brand) &&
      (!ext(`StrStartsWith`, p.p_type, medpol)) &&
      (
        (p.p_size ==  49) ||
        (p.p_size ==  14) ||
        (p.p_size ==  23) ||
        (p.p_size ==  45) ||
        (p.p_size ==  19) ||
        (p.p_size ==  3) ||
        (p.p_size ==  36) ||
        (p.p_size ==  9)
      )
    ) then
      { p.p_partkey -> < _ = p.p_brand, _ = p.p_type, _ = p.p_size > }
    else
      { }

let s_h =
  sum_unique(<s,s_v> <- supplier)
    let idx_customer = ext(`FirstIndex`, s.s_comment, _customer)
    if((idx_customer != -1) && (idx_customer + nchars_customer <= ext(`FirstIndex`, s.s_comment, _complaints))) then
      { s.s_suppkey -> < _ = s.s_suppkey > }
    else
      { }

let ps_h =
  sum(<ps,ps_v> <- partsupp)
    if ((p_h(ps.ps_partkey) != {}) && (!(s_h(ps.ps_suppkey) != {}))) then
    {
      <
        brand = p_h(ps.ps_partkey)(0),
        type = p_h(ps.ps_partkey)(1),
        size = p_h(ps.ps_partkey)(2)
      > ->
      { ps.ps_suppkey -> 1 }
    }
    else
      { }

sum_unique(<k,v> in ps_h)
  { concat(k, < _ = ext(`Size`, v) >) -> 1 }
