let supplier = load[{<s_suppkey: int, s_name: varchar(25), s_address: varchar(40), s_nationkey: int, s_phone: varchar(15), s_acctbal: double, s_comment: varchar(101)> -> int}]("datasets/tpch/supplier.tbl")

let partsupp = load[{<ps_partkey: int, ps_suppkey: int, ps_availqty: double, ps_supplycost: double, ps_comment: varchar(199)> -> int}]("datasets/tpch/partsupp.tbl")

let nation = load[{<n_nationkey: int, n_name: varchar(25), n_regionkey: int, r_comment: varchar(152)> -> int}]("datasets/tpch/nation.tbl")

let germany = ext(`ConstantString`,"GERMANY", 8)

let n_h =
  sum_unique(<n,n_v> in nation)
    if(n.n_name == germany) then
      { n.n_nationkey -> < _ = n.n_nationkey > }
    else
      { }

let s_h =
  sum_unique(<s,s_v> in supplier)
    if(n_h(s.s_nationkey) != {}) then
      { s.s_suppkey -> 1 }
    else
      { }

let ps_t =
  sum(<ps,ps_v> in partsupp)
    if(s_h(ps.ps_suppkey) != {}) then
      <
        _ = ps.ps_supplycost * ps.ps_availqty * 0.0001,
        _ = { ps.ps_partkey -> ps.ps_supplycost * ps.ps_availqty }
      >
    else
      < >

sum(<ps_partkey,ps_supplycost> in ps_t(1))
  if(ps_t(0) < ps_supplycost) then
    { < _ = ps_partkey, _ = ps_supplycost > -> 1 }
  else
    { }
