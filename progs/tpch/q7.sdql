let supplier = load[{<s_suppkey: int, s_name: varchar(25), s_address: varchar(40), s_nationkey: int, s_phone: varchar(15), s_acctbal: double, s_comment: varchar(101)> -> int}]("datasets/tpch/supplier.tbl")

let lineitem = load[{<l_orderkey: int, l_partkey: int, l_suppkey: int, l_linenumber: int, l_quantity: double, l_extendedprice: double, l_discount: double, l_tax: double, l_returnflag: varchar(1), l_linestatus: varchar(1), l_shipdate: date, l_commitdate: date, l_receiptdate: date, l_shipinstruct: varchar(25), l_shipmode: varchar(10), l_comment: varchar(44)> -> int}]("datasets/tpch/lineitem.tbl")

let orders = load[{<o_orderkey: int, o_custkey: int, o_orderstatus: varchar(1), o_totalprice: double, o_orderdate: date, o_orderpriority: varchar(15), o_clerk: varchar(15), o_shippriority: int, o_comment: varchar(79)> -> int}]("datasets/tpch/orders.tbl")

let customer = load[{<c_custkey: int, c_name: varchar(25), c_address: varchar(40), c_nationkey: int, c_phone: varchar(15), c_acctbal: double, c_mktsegment: varchar(10), c_comment: varchar(117)> -> int}]("datasets/tpch/customer.tbl")

let nation = load[{<n_nationkey: int, n_name: varchar(25), n_regionkey: int, r_comment: varchar(152)> -> int}]("datasets/tpch/nation.tbl")

let france = ext(`ConstantString`,"FRANCE", 7)
let germany = ext(`ConstantString`,"GERMANY", 8)

let nationkey_to_name =
  sum_unique(<n,n_v> in nation)
    if((n.n_name == france) || (n.n_name == germany)) then
      { n.n_nationkey -> < _ = n.n_name > }
    else
      { }

let custkey_to_name =
  sum_unique(<c,c_v> in customer)
    if(nationkey_to_name(c.c_nationkey) != {}) then
      { c.c_custkey -> nationkey_to_name(c.c_nationkey)(0) }
    else
      { }

let orderkey_to_name =
  sum_unique(<o,o_v> in orders)
    if(custkey_to_name(o.o_custkey) != {}) then
      { o.o_orderkey -> custkey_to_name(o.o_custkey) }
    else
      { }

let suppkey_to_name =
  sum_unique(<s,s_v> in supplier)
    if(nationkey_to_name(s.s_nationkey) != {}) then
      { s.s_suppkey -> nationkey_to_name(s.s_nationkey)(0) }
    else
      { }

let l_h =
  sum(<l,l_v> in lineitem)
    if(
        (date(19950101) <= l.l_shipdate) && (l.l_shipdate <= date(19961231)) &&
        (orderkey_to_name(l.l_orderkey) != {}) && (suppkey_to_name(l.l_suppkey) != {}) &&
        (
          ((orderkey_to_name(l.l_orderkey) == france) && (suppkey_to_name(l.l_suppkey) == germany)) ||
          ((orderkey_to_name(l.l_orderkey) == germany) && (suppkey_to_name(l.l_suppkey) == france))
        )
    ) then
      {
        <
          supp_nation = suppkey_to_name(l.l_suppkey),
          cust_nation = orderkey_to_name(l.l_orderkey),
          l_year = l.l_shipdate / 10000
        > -> <
          revenue = l.l_extendedprice * (1.0 - l.l_discount)
        >
      }
    else
      { }

sum_unique(<k,v> <- l_h)
  { < _ = k, _ = v > -> 1 }
