let part = load[{<p_partkey: int, p_name: varchar(55), p_mfgr: varchar(25), p_brand: varchar(10), p_type: varchar(25), p_size: int, p_container: varchar(10), p_retailprice: double, p_comment: varchar(23)> -> int}]("datasets/tpch/part.tbl")

let supplier = load[{<s_suppkey: int, s_name: varchar(25), s_address: varchar(40), s_nationkey: int, s_phone: varchar(15), s_acctbal: double, s_comment: varchar(101)> -> int}]("datasets/tpch/supplier.tbl")

let lineitem = load[{<l_orderkey: int, l_partkey: int, l_suppkey: int, l_linenumber: int, l_quantity: double, l_extendedprice: double, l_discount: double, l_tax: double, l_returnflag: varchar(1), l_linestatus: varchar(1), l_shipdate: date, l_commitdate: date, l_receiptdate: date, l_shipinstruct: varchar(25), l_shipmode: varchar(10), l_comment: varchar(44)> -> int}]("datasets/tpch/lineitem.tbl")

let orders = load[{<o_orderkey: int, o_custkey: int, o_orderstatus: varchar(1), o_totalprice: double, o_orderdate: date, o_orderpriority: varchar(15), o_clerk: varchar(15), o_shippriority: int, o_comment: varchar(79)> -> int}]("datasets/tpch/orders.tbl")

let customer = load[{<c_custkey: int, c_name: varchar(25), c_address: varchar(40), c_nationkey: int, c_phone: varchar(15), c_acctbal: double, c_mktsegment: varchar(10), c_comment: varchar(117)> -> int}]("datasets/tpch/customer.tbl")

let nation = load[{<n_nationkey: int, n_name: varchar(25), n_regionkey: int, r_comment: varchar(152)> -> int}]("datasets/tpch/nation.tbl")

let region = load[{<r_regionkey: int, r_name: varchar(25), r_comment: varchar(152)> -> int}]("datasets/tpch/region.tbl")

let steel = ext(`ConstantString`,"ECONOMY ANODIZED STEEL", 23)
let america = ext(`ConstantString`,"AMERICA", 8)
let brazil = ext(`ConstantString`,"BRAZIL", 7)

let r_h =
  sum(<r,r_v> in region)
    if(r.r_name == america) then
      { unique(r.r_regionkey) -> < _ = r.r_regionkey > }
    else
      { }

let n_h =
  sum(<n,n_v> in nation)
    if(r_h(n.n_regionkey) != {}) then
      { unique(n.n_nationkey) -> 1 }
    else
      { }

let nationkey_to_name =
  sum(<n,n_v> in nation)
    { unique(n.n_nationkey) -> < _ = n.n_name > }

let s_h =
  sum(<s,s_v> in supplier)
    { unique(s.s_suppkey) -> < _ = s.s_nationkey > }

let c_h =
  sum_vec(<c,c_v> in customer)
    { c.c_custkey -> c.c_nationkey }

let p_h =
  sum(<p,p_v> in part)
      if(p.p_type == steel) then
        { unique(p.p_partkey) -> < _ = p.p_partkey > }
      else
        { }

let o_h =
  sum(<o,o_v> in orders)
      if((date(19950101) <= o.o_orderdate) && (o.o_orderdate <= date(19961231))) then
        { unique(o.o_orderkey) -> < _ = o.o_custkey, _ = o.o_orderdate > }
      else
        { }

let l_h =
  sum(<l,l_v> in lineitem)
      if((p_h(l.l_partkey) != {}) && (o_h(l.l_orderkey) != {}) && (n_h(c_h(o_h(l.l_orderkey)(0))) != {})) then
        let orderdate = o_h(l.l_orderkey)(1)
        let orderyear = orderdate / 10000
        let volume = l.l_extendedprice * (1.0 - l.l_discount)
        let brazil_volume = if(nationkey_to_name(s_h(l.l_suppkey)(0))(0) == brazil) then volume else 0.0
        { orderyear -> < _ = brazil_volume, _ = volume > }
      else
        { }

sum(<k,v> in l_h)
  { unique(< _ = k, _ = v(0) / v(1) >) -> 1 }
